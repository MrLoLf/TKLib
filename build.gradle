buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

project.ext {
    modid = 'tklib'
    group = 'com.technicalitiesmc'
    name = 'lib'
    archiveName = 'TKLib'
    author = 'amadornes'

    mixin = true
    accessTransformer = true
    javaVersion = 17
}

apply from: 'generic_build.gradle'

sourceSets {
    jeiCompat {
        java.srcDir      project.file('src/compat/jei/java')
        resources.srcDir project.file('src/compat/jei/resources')
    }
}

configurations {
    jeiCompatImplementation.extendsFrom implementation
}

repositories {
    // Maven repo for JEI
    ext.maven 'https://modmaven.dev', 'mezz.jei'
}

dependencies {
    jeiCompatCompileOnly sourceSets.main.output
    jeiCompatCompileOnly fg.deobf("mezz.jei:jei-${_mc_version}:${_jei_api_version}:api")
    runtimeOnly sourceSets.jeiCompat.output
}

jar {
    from sourceSets.jeiCompat.output
    dependsOn jeiCompatClasses
}
